#!/bin/bash
# -*- scheme -*-
# wl07 - wayland client in fibers

LD_LIBRARY_PATH=`pwd`:$LD_LIBRARY_PATH
export LD_LIBRARY_PATH

GUILE_LOAD_PATH=`pwd`:$GUILE_LOAD_PATH
export GUILE_LOAD_PATH

#exec gdb --args guile $0
exec guile $0
!#
;; TODO
;;   add a layer somehow to callers don't need to pass rq-iobuf 0
;;   need a way to block the sender until a number of messages have
;;   built up

(use-modules (rnrs bytevectors))
(use-modules (system foreign))
(use-modules (ice-9 atomic))

(use-modules (wl-client))
(use-modules (sockmsg))

(use-modules (ice-9 pretty-print))
;;(define (sf fmt . args) (apply simple-format #t fmt args))
(define (pp exp) (pretty-print exp))

(define (sferr fmt . args) (apply simple-format (current-error-port) fmt args))
(define (pperr exp) (pretty-print exp (current-error-port)))

;; ============================================================================

(define sys-sleep sleep)

(use-modules ((fibers) #:renamer (lambda (s) (if (eq? s 'sleep) 'fsleep s))))
(use-modules (fibers scheduler))
(use-modules (fibers conditions))
(use-modules (fibers channels))

;;(use-modules (ice-9 readline))
;;(use-modules (ice-9 top-repl))
(use-modules (system repl server))
(use-modules (system repl coop-server))

(define (make-counter init)
  (let ((box (make-atomic-box init)))
    (case-lambda
      (() (let loop ((exp (atomic-box-ref box)))
            (let* ((cur (atomic-box-compare-and-swap! box exp (1+ exp))))
              (if (eq? cur exp) cur (loop (atomic-box-ref box))))))
      ((val) (atomic-box-set! box val)))))

;; ============================================================================

;; per-interface vectors of handlers by opcode
(define wl-handler-vec-vec (make-wl-handler-vec-vec))

;; number of objects 
;;(define *obj-count* (make-parameter 0))
(define *obj-count* (make-counter 0))
(define (obj-count++) (*obj-count*))

;; name -> id
(define object-dict (make-hash-table 97))

;; vector of object-id => ref into wl-handler-vec (handler by opcode)
(define object-decoders-vec (make-vector 1000))

;; vector of object-id => ref into wl-handler-vec (handler by opcode)
(define object-handlers-vec (make-vector 1000))

;; vector of objec-id => user-defined value
(define object-value-vec (make-vector 1000))

;; set-event-handler 'wl_displaly 'get_registry proc => prev-proc
(define (set-event-handler interface event proc)
  (let* ((if-indx (assq-ref wayland-index-dict interface))
         (opcode (assq-ref (vector-ref wayland-opcode-dict-vec if-indx) event))
         (if-handlers (vector-ref wl-handler-vec-vec if-indx))
         (evt-handler (vector-ref if-handlers opcode)))
    (vector-set! if-handlers opcode proc)
    evt-handler))

(define (dispatch obj-id opcode bv ix cm)
  (let* ((dec-vec (vector-ref object-decoders-vec obj-id))
         (decoder (and (vector-ref dec-vec opcode)))
         (hlr-vec (vector-ref object-handlers-vec obj-id))
         (handler (and (vector-ref hlr-vec opcode))))
    (if (and decoder handler)
        (call-with-values (lambda () (decoder obj-id bv ix cm)) handler)
        (begin
          (sferr "dispatch: missing decoder or handler: id=~S op=~S\n"
                 obj-id opcode)
          (sferr "  dec-vec?=~S decoder?=~S hlr-vec?=~S handler?=~S\n"
                 (and dec-vec #t) (and decoder #t)
                 (and hlr-vec #t) (and handler #t))))))

(define rq-chan #f)                     ; channel for issuing requests

(define null-id 0)
(define display-id 1)
(define registry-id 2)
(define registry (make-hash-table))
(define shm-pool-id 3)
(define shm-id 4)

(define wl-sock #f)
(define rq-iobuf #f)
(define ev-iobuf #f)

(define (get-registry)
  (put-message rq-chan
    (lambda () (encode-wl_display:get_registry 1 rq-iobuf 0 2))))

;; --- wl_display

;; wl_display:error
(define (handle-wl_display:error obj-id object_id code message)
  (sferr "display-error: id=~S code=~S msg=~S\n" object_id code message))

;; wl_display:delete_id
(define (handle-wl_display:delete_id obj-id id)
  (sferr "delete-id: ~S\n" id))

;; --- wl_registry

(define (handle-wl_registry:global obj-id name interface version)
  ;; uint string uint
  ;;(sferr "global: name=~S interface=~S version=~S\n" name interface version)
  (hash-set! registry interface (vector name interface version)))

(define (wl-bind interface)
  ;;(sferr "bind ~S\n" interface)
  (let* ((info (hash-ref registry interface))
         (name (vector-ref info 0))
         (version (vector-ref info 2))
         (my-id (obj-count++))
         (my-sy (string->symbol interface)))
    ;;(sferr "  name=~S version=~S my-id=~S\n" name version my-id)
    (hashq-set! object-dict my-sy my-id)
    (set-object-handler my-id my-sy)
    (put-message rq-chan
      (lambda ()
        (encode-wl_registry:bind
         registry-id rq-iobuf 0 name interface version my-id)))))

;; --- wl_shm_pool

(define (wl_shm_pool:create_buffer id offset width height stride format)
  (put-message rq-chan
    (lambda ()
      (encode-wl_shm_pool:create_buffer
       id rq-iobuf 0 id offset width height stride format))))

;; wl_shm_pool::destroy
;; wl_shm_pool::resize

;; --- wl_shm

;; argb8888 = 0
;; xrgb8888 = 1

(define (wl_shm:create_pool id fd size)
  (put-message rq-chan
    (lambda () (encode-wl_shm:create_pool id rq-iobuf 0 id fd size))))

;; informs of allowed formats
(define formats '())

(define (handle-wl_shm:format obj-id format)
  ;; format is uint enum format
  ;; (sferr "format ~A\n" format)
  (set! formats (cons format formats)))

;; =============================================================================

(define socket-path
  (let ((dir (getenv "XDG_RUNTIME_DIR"))
	(dpy (getenv "WAYLAND_DISPLAY")))
    (and dir dpy (string-append dir "/" dpy))))

(define (connect-display)
  (let* ((path socket-path)
         (style (logior SOCK_STREAM SOCK_CLOEXEC))
         (sock (socket PF_UNIX style 0))
         (conn (connect sock AF_UNIX path)))
    ;;(setvbuf sock 'none)
    (fcntl sock F_SETFL (logior O_NONBLOCK (fcntl sock F_GETFL)))
    (set! rq-iobuf (make-bytevector 1024))
    (set! ev-iobuf (make-bytevector 1024))
    sock))

(define (set-object-handler obj-id obj-name)
  (let* ((ob-indx obj-id)
         (ob-name (if (string? obj-name) (string->symbol obj-name) obj-name))
         (if-indx (assq-ref wayland-index-dict ob-name))
         (if-decoders (vector-ref wl-decoder-vec-vec if-indx))
         (if-handlers (vector-ref wl-handler-vec-vec if-indx)))
    (vector-set! object-decoders-vec ob-indx if-decoders)
    (vector-set! object-handlers-vec ob-indx if-handlers)))

(define (install-handlers)
  (set-event-handler 'wl_display 'error handle-wl_display:error)
  (set-event-handler 'wl_display 'delete_id handle-wl_display:delete_id)
  (set-event-handler 'wl_registry 'global handle-wl_registry:global)
  (set-event-handler 'wl_shm 'format handle-wl_shm:format)
  
  ;; display
  (set! display-id 1)
  (set-object-handler display-id 'wl_display)

  ;; registry object
  (set! registry-id 2)
  (set-object-handler registry-id 'wl_registry)
  
  (*obj-count* 3))


;; double-buffer window ARGB32 500x300 = 2*4*500x300 = 1,200,000
;; => bytevector
(define guardian (make-guardian))
(define shm-fd #f)
(define mmap-bv #f)

(define* (setup-buffer-pool #:optional (size #x1000000))
  (let* ((port (tmpfile))(fd (port->fdes port)))
    (guardian port)
    (set! shm-fd fd)
    (truncate-file fd size)
    (set! mmap-bv (mmap 0 size (logior PROT_READ PROT_WRITE) MAP_SHARED fd 0))
    (set! shm-pool-id (obj-count++))
    ;;(sferr "calling create_pool ~S ~S ~S\n" shm-pool-id fd size)
    (wl_shm:create_pool shm-pool-id fd size)))



;; ==================================

(define (sender)
  (sferr "sender starting ...\n")
  (let loop ((n-sent 0) (n-left 0) (cm #f) (rqq '()))
    (cond
     ((positive? n-left)
      (sferr "send message @~S\n" n-sent)
      (sferr " ~A\n" (fmtbv/x rq-iobuf n-sent n-left 4))
      (if cm (sferr " fd ~A\n" (dec-fd cm))
      (catch #t
        (lambda ()
          (let ((n (sendmsg wl-sock rq-iobuf n-sent n-left cm)))
            (force-output wl-sock)
            (loop (+ n-sent n) (- n-left n) #f rqq)))
        (lambda (key . args) (sferr "sender error\n"))))
      )
     ((pair? rqq)
      (call-with-values (car rqq) 
        (lambda (ln cm)
          (sferr "sending msg  ln ~S   cm ~S\n" ln cm)
          (loop 0 ln cm (cdr rqq)))))
     ((get-message rq-chan) =>
      (lambda (req)
        (loop n-sent n-left cm (cons req rqq))))
     (else
      (sferr "sender says wtf\n")))))

(define (receiver)
  (sferr "receiver starting ...\n")
  (let loop ((n-have 0) (object-id #f) (msg-size 8) (opcode #f) (control #f))
    ;;(sferr "n-have=~S  msg-size=~S\n" n-have msg-size)
    (cond
     ((< n-have msg-size)
      (catch 'system-error
        (lambda ()
          (let* ((res (recvmsg! wl-sock ev-iobuf n-have))
                 (n-read (vector-ref res 0))
                 (control (or control (vector-ref res 1)))
                 (flags (vector-ref res 2)))
            (when (zero? n-read) (fsleep 0.1)) ; SLEEP HERE
            (loop (+ n-have n-read) object-id msg-size opcode control)))
        (lambda (key . args) (sferr "receiver error\n"))))
     ((not object-id)
      (let* ((object-id (bytevector-u32-native-ref ev-iobuf 0))
             (word1 (bytevector-u32-native-ref ev-iobuf 4))
             (msg-size (bytevector-u16-native-ref ev-iobuf msg-size-offset))
             (opcode (bytevector-u16-native-ref ev-iobuf opcode-offset)))
        (loop n-have object-id msg-size opcode control)))
     (else
      ;;(sferr "dispatch ~S ~S\n" object-id opcode)
      (dispatch object-id opcode ev-iobuf 8 control)
      (if (> n-have msg-size)
          (bytevector-copy! ev-iobuf msg-size ev-iobuf 0 (- n-have msg-size)))
      (loop (- n-have msg-size) #f 8 opcode control)))))

(define (monitor)
  (let* ((server (spawn-coop-repl-server)))
    (let loop ()
      (poll-coop-repl-server server)
      (yield-current-task)
      (loop))))

(define done-cond #f)

(define (done)
  (and done-cond (signal-condition! done-cond)))

(run-fibers
 (lambda ()
   (set! done-cond (make-condition))
   (set! rq-chan (make-channel))
   (set! wl-sock (connect-display))
   (install-handlers)
   ;;
   (spawn-fiber receiver)
   (spawn-fiber sender)
   (get-registry)
   (fsleep 1)
   (wl-bind "wl_shm")
   (fsleep 1)
   (setup-buffer-pool)
   ;;
   (spawn-fiber monitor)
   (wait done-cond)
   (close-port wl-sock))
 #:hz 0 #:install-suspendable-ports? #f)

;; --- last line ---
